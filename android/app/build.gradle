plugins {
    id 'com.android.application'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

def getGitBranch = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--abbrev-ref', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 26
        targetSdkVersion 31

        applicationId "com.aws.greengrass.nucleus"
        versionCode 2
        versionName '1.0.2' + ' commit:' + getGitHash() + ' branch:' + getGitBranch()
    }

    signingConfigs {
        release {
            storeFile file("keystore_nucleus.jks")
            storePassword "123nucleus456"
            keyAlias "nucleus"
            keyPassword "123nucleus456"
        }
    }

    buildTypes {
        release {
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    /* httpclient-4.5.13.jar and httpcore-4.4.15.jar have conflict on that file */
    packagingOptions {
        exclude 'META-INF/*'
        exclude 'codegen-resources/*'
    }

    buildFeatures {
        viewBinding true
    }

    configurations {
        all {
            // "com.aws.greengrass:logging" already has "ch.qos.logback"
            exclude group: "ch.qos.logback", module: "logback-classic"

            // "aws-iot-device-sdk" already has "org.slf4j"
            exclude group: "org.slf4j", module: "slf4j-api"
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "x86_64", "armeabi-v7a", "arm64-v8a"
            universalApk true
        }
    }
}

dependencies {
    implementation(project(":greengrass-nucleus-android"))
    implementation 'com.aws.greengrass.android.component:base-library:1.0.5'

    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.5.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'com.vdurmont:semver4j:3.1.0'
    implementation 'com.aws.greengrass:logging:2.1.1-SNAPSHOT'      // CloudFront
    def jacksonVersion = "2.13.0"
    implementation "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
}
