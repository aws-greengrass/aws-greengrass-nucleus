import java.nio.file.Paths


apply plugin: 'checkstyle'
apply plugin: "com.github.hierynomus.license"

ext.configPath = { baseDir, reportDir, filename ->
    java.nio.file.Path path = Paths.get(baseDir)
    String parentConfigPath
    try {
        parentConfigPath = Paths.get( path.parent.toString(), reportDir, filename).toString()
    } catch (IllegalArgumentException e) {
        parentConfigPath = baseProject
    }
    return parentConfigPath

}

task checkstyle(type: Checkstyle) {
        configFile = file(configPath("${project.rootDir}", "codestyle", "checkstyle.xml"))
        configProperties.checkstyleSuppressionsPath = file(configPath("${project.rootDir}", "codestyle", "checkstyle-suppressions.xml")).absolutePath
        maxWarnings = 0
        ignoreFailures = false
        showViolations = true
        source 'src'
        include '**/*.java'
        exclude '**/gen/**'

        classpath = files()
        reports {
            html.destination project.file('build/reports/checkstyle.html')
        }
}


license {
    header = file(configPath("${project.rootDir}", ".license", "header.txt"))
    headerDefinitions {
        java_style {
            firstLine = "/*"
            endLine = "*/EOL"
            beforeEachLine = " * "
            firstLineDetectionPattern = "(\\\\s|\\\\t)*/\\\\*.*\$"
            lastLineDetectionPattern = ".*\\\\*/(\\\\s|\\\\t)*\$"
            allowBlankLines = false
            isMultiline = false
            padLines = false
        }
        batch {
            firstLine = "@REM -------------------------------"
            endLine = "@REM -------------------------------"
            beforeEachLine = "@REM "
            firstLineDetectionPattern = "@REM(\\\\s|\\\\t)*.*\$"
            lastLineDetectionPattern = "@REM(\\\\s|\\\\t)*.*\$"
            allowBlankLines = false
            isMultiline = false
            padLines = false
        }
    }
    mapping {
        java = 'java_style'
        bat = 'batch'
    }
    excludes(["*", "src/main/AndroidManifest.xml", ".gradle/**", "bin/**", "build/**",
              "codestyle/**", "src/test/greengrass-nucleus-benchmark/**",
              "src/*/resources/**", "src/**/vendored/**"])

}


check.dependsOn 'checkstyle'
